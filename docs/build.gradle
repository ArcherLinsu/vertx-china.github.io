import org.apache.commons.codec.digest.DigestUtils

buildscript {
  dependencies {
    classpath "commons-codec:commons-codec:1.15"
  }
}

plugins {
  id 'java'
}

ext {
  versions = [
    "3.9.7",
    "3.9.6",
    "3.9.5",
    "3.9.4",
    "3.9.3",
    "3.9.2",
    "3.9.1",
    "3.9.0",
    "3.8.5",
    "3.8.4",
    "4.0.0",
    "4.0.2",
    "4.0.3",

    // latest GA release (must be the last entry)
    "4.1.0"
  ]
}

repositories {
  jcenter()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots/"
    mavenContent {
      snapshotsOnly()
    }
  }
}

dependencies {
  implementation("io.vertx:vertx-stack-docs:${getVersionName(versions.last())}:docs@zip") {
    transitive = false
    changing = getVersionName(versions.last()).endsWith("-SNAPSHOT")
  }
}

def getVersionName(version) {
  if (version instanceof Map) {
    return version.version
  }
  return version
}

def getVersionPath(version) {
  if (version instanceof Map) {
    return version.path
  }
  return version
}

def versionToConfiguration(version) {
  return "implementation_${getVersionName(version).replace('.', '_')}"
}

versions.each { configurations.create(versionToConfiguration(it)) }
versions.each { v ->
  dependencies.add(versionToConfiguration(v), "io.vertx:vertx-stack-docs:${getVersionName(v)}:docs@zip") {
    transitive = false
    changing = getVersionName(v).endsWith("-SNAPSHOT")
  }
}

def writeHash(sourceFile, version) {
  def outDir = new File(projectDir, "hashes")
  outDir.mkdirs()
  def outFile = new File(outDir, "${getVersionPath(version)}.sha")
  outFile.text = new DigestUtils("sha-256").digestAsHex(sourceFile)
}
String[] excludePaths = ["**/apidocs/**", "**/jsdoc/**", "**/kdoc/**", "**/scaladocs/**"]
task extractDocsLatest(type: Sync) {
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.collect { zipTree(it) }
  }
  exclude(excludePaths)
  into "$projectDir/extracted"
  configurations.runtimeClasspath.each { writeHash(it, "latest") }
}

task extractDocs {
  dependsOn extractDocsLatest
}

versions.take(versions.size()).each { version ->
  def conf = configurations[versionToConfiguration(version)]
  def t = tasks.create(name: "extractDocs_${getVersionName(version).replace('.', '_')}", type: Sync) {
    dependsOn conf
    from {
      conf.collect { zipTree(it) }
    }
    into "$projectDir/extracted/${getVersionPath(version)}"
    exclude(excludePaths)
    conf.each { writeHash(it, version) }
  }
  extractDocs.dependsOn t
}

task copyApidocs(type: Sync) {
  dependsOn extractDocs
  from "$projectDir/extracted"
  into "$projectDir/../public/docs"
  include "**/apidocs/**"
  includeEmptyDirs false
  preserve {
    include ".gitkeep"
  }
}
// 用来拷贝英文文档到translation目录的，主要是在english分支使用
task copyEnglish(type: Copy) {
  dependsOn extractDocs
  from "$projectDir/extracted"
  into "$projectDir/translation"
  include "**/*.adoc"
  exclude "**/dataobjects.adoc"
  exclude "**/enums.adoc"
  exclude {
      return it.path.matches("\\d+\\.\\d+\\.\\d+/.*")
  }
  includeEmptyDirs false
}

clean {
  delete "$projectDir/extracted"
  delete copyApidocs.outputs.files.findResults { it.listFiles() }
    .flatten()
    .findAll { it.name != ".gitkeep" }
}

defaultTasks 'extractDocs', 'copyEnglish'
